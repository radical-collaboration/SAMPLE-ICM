#!/bin/csh -fx

#=======================================================================

# Overall control script for performing ice-model ensembles.
# Loops over calvliq and cliffvmax (foreach loops).
# 1. Makes new (sub)directory for each run. 
# 2. Copies input files and makefile to the run directory.
# 3. Edits makefile for this run, compiles and links executable.
# 4. Starts this run in the background.

# Files needed in "root" directory:
# makeiceroot, copied to make_...
# restart_..., copied to restartin.
# crhmelfile_..., copied to crhmelfilein (basal sliding coeff. map).

#=======================================================================

set rootdir = $cwd                     # set "root" directory to current

# Loops for values of the ensemble parameters.   
# These values of calvliq and cliffvmax are as in the 2016 Nature 
# paper. You can change the ranges and number of values as you like.

# vhs edit: Arguments to be passed through command-line (RADICAL)
set calvliq = $1 # m per (m/yr)^2
set cliffvmax = $2 # km/year

set calvliqens = "CALVLIQ=$calvliq"
set cliffvmaxens = "CLIFFVMAX=${cliffvmax}.e3"         # km/yr to m/yr

# Set overall switch for cliff or no-cliff failure physics.
# Not necessary because -DCLIFFMELT with -DCLIFFVMAX=0 is equivalent
# to not setting CLIFFMELT, but saves some run time.
if ($cliffvmax == 0) then 
  set cliffmeltens = NOCLIFFMELT
else 
  set cliffmeltens = CLIFFMELT
endif

# unique run label, used for subdirectory, makefile and exe names:
set runlab = C${calvliq}_V${cliffvmax}

# start each loop in the root directory: 
cd $rootdir

# create subdirectory for this run, cd into it:
set rundir = Run_$runlab
(rm -f -r $rundir) >& /dev/null
mkdir $rundir
cd $rundir

# copy input files into run directory:
cp -p $rootdir/restart_10p restartin #$rootdir/restart_inv10d restartin        # modern initial state
cp -p $rootdir/crhmelfile_inv10d crhmelfilein  # basal sliding coef. map

# copy "root" makefile into run directory, edit switches for this run:
cp -p $rootdir/makeiceroot maketmp
sed \
    -e "s/CALVLIQENS/$calvliqens/g" \
    -e "s/CLIFFMELTENS/$cliffmeltens/g" \
    -e "s/CLIFFVMAXENS/$cliffvmaxens/g" \
    maketmp >! maketmp2
if ($status != 0) exit
mv maketmp2 make_$runlab
(rm -f maketmp) >& /dev/null

# compile and link executable file sheetshelf.exe:
gmake -f make_$runlab
if ($status != 0) exit

# rename executable file to a unique name (helps with ps ...):
mv sheetshelf.exe sheet_$runlab.exe

# start model running in background:
./sheet_$runlab.exe #>&! x &

exit
